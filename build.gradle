apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'core.demo'
    version = '1.0.0'

    repositories {
        maven {
            url 'https://neowu.github.io/maven-repo/'
            content {
                includeGroupByRegex 'core\\.framework.*'
            }
        }
    }

    sourceSets {
        dev {
            java {
                srcDir 'src/dev/java'
            }
            compileClasspath += sourceSets.main.runtimeClasspath
            runtimeClasspath += sourceSets.main.runtimeClasspath
        }
    }
}

def coreNGVersion = '8.0.5'
def hsqlVersion = '2.6.0'
def jacksonVersion = '2.13.3'
def mysqlVersion = '8.0.29'

configure(subprojects.findAll { it.name.endsWith('-db-migration') }) {
    apply from: file("${rootDir}/gradle/db-migration.gradle")

    dependencies {
        runtimeOnly "mysql:mysql-connector-java:${mysqlVersion}"
        runtimeOnly 'org.postgresql:postgresql:42.4.0'
    }
}

configure(subprojects.findAll { it.name.endsWith('-es-migration') }) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        implementation "core.framework:core-ng-search:${coreNGVersion}"
    }
    tasks.register('esMigrate') {
        dependsOn run
    }
}

configure(subprojects.findAll { it.name.endsWith('-mongo-migration') }) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        implementation "core.framework:core-ng-mongo:${coreNGVersion}"
    }
    tasks.register('mongoMigrate') {
        dependsOn run
    }
}

configure(subprojects.findAll { it.name.endsWith('-service-interface') }) {
    dependencies {
        implementation "core.framework:core-ng-api:${coreNGVersion}"
    }
}

configure([project(':demo-service'),
           project(':demo-site')]) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

project(':demo-service') {
    dependencies {
        implementation project(':demo-service-interface')
        implementation "core.framework:core-ng-mongo:${coreNGVersion}"
        implementation "core.framework:core-ng-search:${coreNGVersion}"
        testImplementation "core.framework:core-ng-mongo-test:${coreNGVersion}"
        testImplementation "core.framework:core-ng-search-test:${coreNGVersion}"
        runtimeOnly "mysql:mysql-connector-java:${mysqlVersion}"
        runtimeOnly 'org.postgresql:postgresql:42.4.0'
        testRuntimeOnly "org.hsqldb:hsqldb:${hsqlVersion}"
    }
}

project(':demo-site') {
}

project(':benchmark') {
    dependencies {
        implementation 'org.openjdk.jmh:jmh-generator-annprocess:1.27'
        implementation "core.framework:core-ng:${coreNGVersion}"
        implementation 'io.undertow:undertow-core:2.3.0.Final'
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
        implementation "com.fasterxml.jackson.module:jackson-module-afterburner:${jacksonVersion}"
        implementation "com.fasterxml.jackson.module:jackson-module-blackbird:${jacksonVersion}"
    }
}

project(':kibana-generator') {
    dependencies {
        implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    }
}
